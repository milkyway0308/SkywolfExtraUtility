plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0" apply false
    id "org.jetbrains.kotlin.jvm" version "1.5.31" apply false
    id "java"
    id "idea"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "org.jetbrains.kotlin.jvm"

    repositories {
        mavenCentral()
        maven {
            url = uri(rootProject.properties["reposilite.release"])
            allowInsecureProtocol(true)
        }
        maven {
            url = uri(rootProject.properties["reposilite.spigot"])
            allowInsecureProtocol(true)
        }
    }

    jar {
        archiveBaseName.set("SkywolfExtraUtility-${project.name.capitalize()}")
        destinationDirectory = file("$rootDir/addonJarsTemp")
        finalizedBy("shadowJar")
    }

    shadowJar {
        archiveBaseName.set("SkywolfExtraUtility-${project.name.capitalize()}")
        destinationDirectory = file("$rootDir/addonJars")
        classifier("")
    }

    processResources {
        filesMatching(["loaderDeclaration.properties", "plugin.yml", "bungee.yml"]*.toString()) {
            expand(["version": rootProject.properties["version"] as String])
        }
        outputs.upToDateWhen { false }
    }
}


gradle.taskGraph.whenReady {
    File file = file("$rootDir/addonJarsTemp")
    file.mkdirs()
    idea {
        module {
            excludeDirs += file
        }
    }
}

gradle.buildFinished {
    delete file("$rootDir/addonJarsTemp")
}